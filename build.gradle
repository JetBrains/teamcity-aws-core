/*
 * Copyright 2000-2022 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'idea'
}

ext {
  teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2022.12-SNAPSHOT'
  awsSDKVersion = findProperty('aws.sdk.version') ?: '1.12.199'

  libVersion = project.hasProperty('awsCoreVersion') ? property('awsCoreVersion') : teamcityVersion
  providedPluginVersion = findProperty('versionNumber')

  spaceUsername = findProperty('space.repository.user') ?: findProperty('spaceUsername')
  spacePassword = findProperty('space.repository.password') ?: findProperty('spacePassword')

  pluginVersion = project.hasProperty('versionNumber') ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmm')
}

allprojects {

  project.repositories.clear()

  repositories {
    mavenLocal()
    maven { url "https://download.jetbrains.com/teamcity-repository" }
    maven {
      url "https://packages.jetbrains.team/maven/p/tc/maven"
      credentials {
        username spaceUsername
        password spacePassword
      }
    }
    mavenCentral()
  }
}

idea {
  project {
    vcs = 'Git'
  }
}

configure(subprojects) {
  apply plugin: 'java'

  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  test {
    useTestNG()
    testLogging.showStandardStreams = true
  }
}

task uploadMavenArtifacts {
  tasks.uploadMavenArtifacts.dependsOn ':aws-core-common:prepareUploadMavenArtifactsTask'
}
