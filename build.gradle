/*
 * Copyright 2000-2022 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'idea'
  id 'groovy'
}

ext {
  teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2021.1'

  teamcityDir = findProperty('teamcityDir') ?: "$rootDir/teamcity/servers/TeamCity-${teamcityVersion}"
  teamcityDataDir = "$rootDir/teamcity/data/" + teamcityVersion
  teamcityJavaHome = System.properties['java.home']

  awsSDKVersion = findProperty('aws.sdk.version') ?: '1.12.199'

  libVersion = project.hasProperty('awsCoreVersion') ? property('awsCoreVersion') : teamcityVersion

  spaceUsername = findProperty('space.repository.user') ?: findProperty('spaceUsername')
  spacePassword = findProperty('space.repository.password') ?: findProperty('spacePassword')
}

allprojects {
  project.repositories.clear()

  repositories {
    mavenLocal()
    maven { url "https://download.jetbrains.com/teamcity-repository" }
    maven {
      url "https://packages.jetbrains.team/maven/p/tc/maven"
      credentials {
        username spaceUsername
        password spacePassword
      }
    }
    mavenCentral()
  }
}

idea {
  project {
    vcs = 'Git'
  }
}

configure(subprojects) {
  apply plugin: 'java'

  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  test {
    useTestNG()
    testLogging.showStandardStreams = true
  }

  dependencies {
    implementation "org.codehaus.groovy:groovy-all:2.2.1"
  }
}

task uploadMavenArtifacts {
  tasks.uploadMavenArtifacts.dependsOn ':aws-core-common:prepareUploadMavenArtifactsTask'
}
